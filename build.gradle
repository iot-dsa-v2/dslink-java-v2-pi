apply plugin: 'application'
apply plugin: 'java'

// Version is taken from dslink.json
build {
    version = new groovy.json.JsonSlurper().parseText(file('dslink.json').text).version
}

mainClassName = 'org.iot.dsa.dslink.DSLink'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    //implementation 'org.iot-dsa:dslink-java-v2-restadapter:1.4.0' //for a locally installed sdk
    implementation 'com.github.iot-dsa-v2:dslink-java-v2-restadapter:1.4.1'
    implementation 'org.apache.cxf:cxf-rt-rs-client:3.4.2'
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name' : 'org.iot.dsa.dslink.pi'
        )
    }
}

applicationDistribution.from(new File(project.projectDir, "/dslink.json"))

distZip {
    archiveName project.name + ".zip"
    eachFile { file ->
        String path = file.relativePath
        file.setPath(path.substring(path.indexOf("/")+1,path.length()))
    }
}

distTar.enabled = false

run {
    args System.getProperty("exec.args", "").split()
    workingDir project.buildDir
}

wrapper {
    gradleVersion = '6.8'
}
